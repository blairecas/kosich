	.mcall	.exit
	.enabl	LC
	.list	MEB	; enable macro-extensions binary listing
	.asect
	.=1000

Vaddr = 20000
Vaden = 120200

; //////////////////////////////////////////////////////////////////////////////
; // START
; //////////////////////////////////////////////////////////////////////////////

Start:	; clear screen
	mov	#MsgCls, R0			; we needed THIS!!! or else keys and print buffer will be stuck
	call	PriStr	
	mov	#Vaddr, R0
	mov	#40100, R3
	clr	(R0)+
	sob	R3, .-2

	; run PPU code
	mov	#1, PPCOMM		; ask PPU for 'phase 1 ready'
	mov	#PPUSTA, R4		; run PPU code
	mov	#<PPUEND-PPUSTA/2>, R5
	call	PPRUN
	tst	PPCOMM			; wait for 'ppu phase 1 ready'
	bne	.-4			; TODO: check timer to not wait forever?
	; set PPU flag to 'running'
	inc	PPCOMM			; PPU will clear this after key is pressed

	; set vsync interrupt for FPS counting
	mov	@#100, VsiOld
	mov	#VsyncI, @#100
	clr	Tick50

	call	Kosich

	; clear PPU RAM
	call	PPRele			; release PPU memory

	; restore vsync interrupt
	mov	VsiOld, @#100

	mov	#Msg000, R0
	call	PriStr

	.exit


; 50Hz interrupt
VsyncI:	inc	Tick50
	rti


; print string with channel 0
; r0 = addr
PriStr:
	movb	(R0)+, R1
	beq	99$
	tstb	@#177564
	bpl	.-4
	movb	r1, @#177566
	br	pristr
99$:	return 


PPcomm:	.word	0	; PPU command
Tick50:	.word	0	; 50Hz ticks counter
Frames:	.word	0	; frames counter
VsiOld:	.word	0	; old vsync interrupt addr

Msg000:	.asciz	/Adaptation of Kosi4ka BK-0010 256-byte intro by Manwe/<57>/SandS/<12><15>
Msg010:	.asciz	/ERR: PPU allocate memory failed/
MsgCls: .byte	033, 014, 110, 0
	.even

MsgFp8:	.asciz	/00:00/
	.even

Font8:	.byte	0, 76, 147, 147, 147, 147, 177, 76
	.byte	0, 30, 34, 34, 30, 30, 176, 176
	.byte	0, 76, 163, 70, 34, 16, 177, 177
	.byte	0, 176, 60, 30, 60, 147, 177, 76
	.byte	0, 3, 3, 73, 177, 70, 70, 70
	.byte	0, 177, 7, 77, 140, 147, 177, 76
	.byte	0, 76, 7, 77, 147, 147, 177, 76
	.byte	0, 177, 161, 70, 34, 16, 7, 7
	.byte	0, 76, 147, 76, 147, 147, 177, 76
	.byte	0, 76, 147, 147, 176, 70, 34, 16
	.byte	0, 0, 0, 0, 0, 30, 30, 0
	.even 

; print string
; R0 - text addr
PriSt8:	mov	#128.+10.+Vaddr, R5
	mov	#64.*2., R2		; vaddr addition is 64 words
10$:	movb	(R0)+, R1		; get text byte
	beq	99$			; zero - exit
	sub	#'0, R1			; relative to '0'
	asl	R1			; *8 bytes (character font size)
	asl	R1
	asl	R1
	add	#Font8, R1		; R1 = font data
	.rept	8.			; out character
	movb	(R1)+, (R5)
	add	R2, R5
	.endr
	sub	#64.*2.*8.-2, R5	; advance vaddr
	br	10$
99$:	return 

; put 2 digits string
; R5 - where to put, R0 - number
Digi2:
	cmp	R0, #99.
	blos	10$
	mov	#99., R0
10$:	movb	#'0, (R5)+
	movb	#'0, (R5)+
20$:	mov	R0, R1
	clr	R0
	div	#10., R0
	add	#'0, R1
	movb	R1, -(R5)
	tst	R0
	bne	20$
	return

; count fps
CouFps:	mov	Frames, R0
	mul	#50., R0
	div	Tick50, R0
	; R0=FPS R1=FPS%TICK50
	mov	R1, -(SP)
	mov	#MsgFp8, R5
	call	Digi2
	mov	(SP)+, R0	; convert %TICK50 to hundreds
	mul	#100., R0	; *100/TICK50 = hundreds
	div	Tick50, R0	;
	mov	#MsgFp8+3, R5
	call	Digi2
	return

; //////////////////////////////////////////////////////////////////////////////
; // CPU -> PPU exchange	
; //////////////////////////////////////////////////////////////////////////////

; PPU message
;
PPMSG:	.WORD	PPARR	; address of beginning of array
        .WORD	177777	; end of transmission

; PPU data exchange array
;
PPARR:	.BYTE	0	; return value (0 - OK)
PPCMD:	.BYTE	0	; command
	.WORD	32	; device type (32 - PPU mem)
PPAPP:	.WORD	0	; address for PPU
PPACP:	.WORD	0	; address for CPU
PPLEN:	.WORD	0	; length in words

; send command to PPU with exch array
PPSEN:	mov	R0, -(SP)
	mov	R1, -(SP)
	mov	#PPMSG, R0	; array address
	mov	#5, R1		; bytes to send+1 (sending from @#PP_MSG)
	br	1$
2$:	movb	(R0)+, @#176676
1$:	tstb	@#176674	; test if we are ready to send
	bpl	1$		; cycle if >= 0 (not set last bit in byte)
	sob	R1, 2$
	mov	(SP)+, R1
	mov	(SP)+, R0
	return

; send and start PPU code
; R4 - start addr in CPU
; R5 - length / 2
PPRUN:	movb	#1, PPCMD	; 1 - allocate memory
	mov	R5, PPLEN
	call	PPSEN
	tstb	PPARR		; test if allocate success
	beq	1$		; 0 - OK
	mov	#Msg010, R0
	call	PriStr
	.exit			; fatal error - out of memory in PPU
1$:	movb	#20, PPCMD	; 20 - write to PPU mem
	mov	R5, PPLEN
	mov 	R4, PPACP
	call	PPSEN
	movb	#30, PPCMD	; 30 - run
	call	PPSEN
	return

; release PPU memory in case of something..
PPRELE:	movb	#2, PPCMD	; 2 - release memory
	call	PPSEN
	return


; //////////////////////////////////////////////////////////////////////////////	
; // PPU first phase 
; //////////////////////////////////////////////////////////////////////////////

PPUSTA:	mov	PC, R0
	add	#INTKB1-., R0		; keyboard interrupt addr
	mov	@#300, BKW300		; replace keyboard interrupt
	mov	R0, @#300
	; construct our own lines table	
	mov	PC, R0
	add	#PPLIN1-., R0
	add	#10, R0			; align addr with 8. bytes
	bic	#7, R0
	mov	R0, R2			; save addr and use later
	; 1st element - set palette (YRGB YRGB YRGB YRGB)
	;                           (0011 0010 0001 0000)
	;                           (0111 0110 0101 0100)
	mov	#^B1001100110001000, (R0)+
	mov	#^B1100110010101010, (R0)+
	clr	(R0)+			; vaddr is not used here
	mov	R0, (R0)		; next element addr
	add	#2, (R0)
	bis	#2, (R0)+		; #2 = 010 - next element is scale&cursor
	; 2nd line - set scale, lumi and cursor
	mov	#^B0000000000010000, (R0)+ ; no cursor
	mov	#^B0000000000000111, (R0)+ ; 640x288, max luminance (..00111-640, ..10111 - 320)
	clr	(R0)+			; vaddr is not used here
	mov	R0, (R0)
	add	#2, (R0)+		; next element is 2-words
	; 16 not used lines
	mov	#16., R3
10$:	clr	(R0)+
	mov	R0, (R0)
	add	#2, (R0)+
	sob	R3, 10$
	; 16 'empty' lines (BK-0010 is 256 lines screen, so 288-256/2 = 16
	mov	#16., R3
	mov	#Vaddr/2, R1
20$:	mov	R1, (R0)+
	mov	R0, (R0)
	add	#2, (R0)+
	sob	R3, 20$
	; next 256 'main' lines
	mov	#Vaddr/2, R1
	mov	#256., R3
30$:	mov	R1, (R0)+
	mov	R0, (R0)
	add	#2, (R0)+
	add	#64., R1		; double BK-0010 scr width
	sob	R3, 30$
	; last 16 empty lines
	mov	#Vaddr/2, R1
	mov	#16., R3
40$:	mov	R1, (R0)+
	mov	R0, (R0)
	add	#2, (R0)+
	sob	R3, 40$
	; set new lines table
	mov	@#270, BKW270		; backup old table
	mov	@#272, BKW272
	clr	@#270
	bis	#6, R2			; first line is palette 
	mov	R2, @#272		; saved addr
	; queue process
	mov	PC, R0			; get proc addr
	add	#PPPGM1-., R0		;
	mov	R0, ADPGM1		; save for later use
	mov	R0, @#7124		; 7104.. (24(8) bytes) - processes addrs
	mov	#1, @#7100		; 7060.. (24(8) bytes) - processes status, 1-run
	; clear current command (phase 1 ready)
	mov	#PPCOMM/2, @#177010
	clr	@#177014
	return

; 'keyboard' interrupt
INTKB1:	mov	R0, -(SP)
	mov	@#177702, R0
	; TODO: change drawing variables in CPU from pressed keys
	bit	#^B10000000, R0
	bne	10$
	inc	KEYPR0
10$:	mov	(SP)+, R0
	rti
KEYPR0:	.word	0

; 'backup' values
BKW270:	.WORD	0
BKW272:	.WORD	0
BKW300:	.WORD	0

; lines table data
PPLIN1:	.BLKW	1200			; need 1150 (616.) words, but for .even 10 and whatever

; //////////////////////////////////////////////////////////////////////////////
; // PPU Process
; //////////////////////////////////////////////////////////////////////////////

ADPGM1:	.WORD	0			; address of process

PPPGM1:	mov	R0, -(SP)		; need to save R0 for PPU process
	tst	KEYPR0			; test for keypress to abort
	bne	10$			;
	; return to dispatcher
	mov	ADPGM1, @#7124		; re-queue process
	mov	#1, @#7100		; set for execution
99$:	mov	(SP)+, R0		; restore R0 (needed for dispatcher)
	jmp	@#174170		; jump to process dispatcher
10$:	; abort PPU process
	mtps	#200
	mov	BKW270, @#270		; restore lines table addr
	mov	BKW272, @#272
	mov	BKW300, @#300		; restore kbd interrupt
	clr	@#7100			; do not execute process again
	mov	#PPCOMM/2, @#177010	; control word addr
	clr	@#177014		; clear it
	mtps	#0
	br	99$

; //////////////////////////////////////////////////////////////////////////////
; // END PPU CODE 
; //////////////////////////////////////////////////////////////////////////////
PPUEND:

	
; //////////////////////////////////////////////////////////////////////////////
; // Manwe/SandS intro code here
; //////////////////////////////////////////////////////////////////////////////

KOSICH:

D1Ang	= 16.				; Cos offset
D1Var	= D1Acc+6

D1Spd:	br	D1Beg			; Y
	.word	36543,1321		; timer2, timer1
D1Acc:

D1Beg:
	; Clone sine table
	mov	#D1TBL+32., R1		; pointer to sine table
	mov	#32., R2
10$:	movb	D1TBL-1(R2), (R1)+
	sob	R2, 10$

Loop:	mov	#D1Acc, R3
	clr	(R3)+			; clear accums
	clr	(R3)+
	tst	(R3)+
	mov	#100, R0		; mask
D1Dir:	incb	(R3)			; default Y
	bne	10$
	xor	R0, D1Dir		; DEC opcode: 5313, INC opcode: 5213
10$:	mov	(R3)+, (R3)		; set Y
	mov	(R3)+, (R3)		; set timer2

	mov	#VAddr+400, R0 		; screen address

	; render twister to screen
Render:
	mov	#D1Spd, R4
	mov	#3, R3
10$:	add	(R4)+, 4(R4)		; add speed to accum
	adc	14(R4)			; increment variable
	sob	R3, 10$
	
	mov	#D1Var+6, R3		; pointer to variable array
	mov	(R3), R4		; get timer 1
	call	D1Sin
	swab	R5
	asl	R5
	mov	R5, D1Spd		; Y speed

	mov	-(R3), R4		; get timer 2
	call	D1Sin
	add	#64., R5		; shift from left side
	asr	R5
	asr	R5
	asl	R5			; in UKNC we are in WORDs
	mov	R5, R2			; horisontal shift

	mov	-(R3), R4		; get Y

	mov	R0, -(SP)		; set maximum
	mov	R0, -(SP)
	add	#128., (SP)		; set minimum

	mov	#8., R3
30$:	call	D1Sin			; R5=Sin(R4)
	mov	R5, R1
	asr	R1			; left coordinate
	add	#D1Ang, R4		; 90 degrees offset
	call	D1Sin			; R5=Sin(R4)
	asr	R5			; right coordinate
	sub	R1, R5			; length
	ble	70$
	bic	#177, R0		; new screen line
	add	R2, R0			; horisontal shift
	add	R1, R0
	add	R1, R0
	cmp	R0, (SP)		; minimum
	bhis	40$
	mov	R0, (SP)		; set new minimum
40$:	mov	D1Clr-2(R3), R1		; read colour
50$:	mov	R1, (R0)+
	sob	R5, 50$
	cmp	R0, 2(SP)		; maximum
	blos	60$
	mov	R0, 2(SP)		; set new maximum
60$:	bis	#^B1111000011110000, -(R0)		; draw border
70$:	dec	R3
	sob	R3, 30$

	mov	(SP)+, R5		; get minimum
	clr	-(R5)			; erase left pixels
	clr	-(R5)
	clr	-(R5)
	clr	-(R5)
	mov	(SP)+, R5		; get maxumum
	clr	(R5)+			; erase right pixels
	clr	(R5)+
	clr	(R5)+			; added one more clear
	clr	(R5)+

	add	#128.*3, R0		; +3 screen lines
	cmp	R0, #Vaden
	blo	Render			; end of screen?

	inc	Frames			; for FPS counting
	cmp	Frames, #50.
	blt	80$
	; print fps
	call	CouFps
	clr	Frames
	clr	Tick50
	mov	#MsgFp8, R0
	call	PriSt8

80$:	tst	PPCOMM			; +++ test for PPU asking to abort
	bne	Loop			; next frame if not

	return

; Sinus
; input: R4 - angle
; outpu: R5 - sinus
D1Sin:	mov	R4, R5
	bic	#177700, R5
	movb	D1Tbl(R5), R5
	return

	; were bytes 231,167,252,273
D1Clr:	.word	^B1010101001010101, ^B0101010111111111
	.word	^B1111111100000000, ^B1111111101010101

D1Tbl:	.byte	63.,62.,62.,61.,60.,58.,56.,54.,52.,50.,48.,45.,42.,39.,37.,33.
	.byte	31.,28.,25.,22.,19.,17.,14.,12.,10.,8.,6.,4.,3.,2.,2.,1.
	.even

	.blkw	32.	; buffer for sinetable cloning

.=Vaden
	.word	1,2,3,4

	.end	Start
